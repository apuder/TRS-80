buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
    }
}

plugins {
    id 'com.android.application'
    id 'com.google.protobuf' version '0.8.18'
}

dependencies {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.haberling.net' }
    }

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:cardview-v7:29.0.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
    implementation 'com.google.guava:guava:27.0.1-android'
    implementation 'com.android.support:support-v4:29.0.0'
    implementation 'com.android.support:design:29.0.0'
    implementation 'com.android.support:mediarouter-v7:29.0.0'
    implementation 'com.android.support:recyclerview-v7:29.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.google.android.gms:play-services-cast:20.1.0'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.simpleframework:simple-http:6.+'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation project(':retrostore')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task syncGeneratedativeProtoFilesDebug(type: Sync) {
    from "$projectDir/src/debug/cpp"
    into "$projectDir/src/main/c/proto"
}
task syncGeneratedativeProtoFilesRelease(type: Sync) {
    from "$projectDir/src/release/cpp"
    into "$projectDir/src/main/c/proto"

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }

    // Generate java and cpp versions of the files.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
                cpp {
                }
            }
            task.finalizedBy(syncGeneratedativeProtoFilesDebug, syncGeneratedativeProtoFilesRelease)
            task.plugins {
                javalite {
                    outputSubDir = "java"
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

android {
    compileSdk 29
    ndkVersion '23.1.7779620'
    defaultConfig {
        applicationId "org.puder.trs80"
        minSdk 16
        targetSdk 30
        versionCode 45
        versionName "0.45"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
            }
        }
    }
    buildTypes {
        debug {
            resValue "bool", "debug", "true"
        }
        release {
            resValue "bool", "debug", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version '3.18.1'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
}

//// Android
//tasks.named("preBuild") { finalizedBy("syncGeneratedativeProtoFilesDebug") }
//tasks.named("preBuild") { finalizedBy("syncGeneratedativeProtoFilesRelease") }
//
//tasks.named("build") { finalizedBy("syncGeneratedativeProtoFilesDebug") }
//tasks.named("build") { finalizedBy("syncGeneratedativeProtoFilesRelease") }


clean {
    // Generates protocol buffer files end up in here.
    delete "$projectDir/src/debug"
    delete "$projectDir/src/release"
}
