apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "org.puder.trs80"
        minSdkVersion 11
        targetSdkVersion 22

        ndk {
            moduleName "xtrs"
        }
    }

    sourceSets.main {
        jni.srcDirs = [] //disable automatic ndk-build call
        //jni.srcDirs = ['src/main/jni']
        jniLibs.srcDir 'src/main/libs'
    }

    buildTypes {
        debug{
            resValue "bool", "debug", "true"
        }
        release {
            resValue "bool", "debug", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }
}

dependencies {
    repositories {
        mavenCentral()
    }
    compile 'com.android.support:design:23.0.+'
    compile 'com.android.support:appcompat-v7:23.0.+'
    compile 'com.android.support:mediarouter-v7:23.0.+'
    compile 'com.android.support:cardview-v7:23.0.+'
    compile 'com.android.support:recyclerview-v7:23.0.+'
    compile 'com.google.android.gms:play-services-cast:+'
    compile 'org.apache.commons:commons-io:1.3.+'
    compile 'org.simpleframework:simple-http:6.+'
    compile 'ch.acra:acra:4.6.+'
    compile files('libs/org.apache.http.legacy.jar')
}
