buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "org.puder.trs80"
        minSdkVersion 12
        targetSdkVersion 22

        ndk {
            moduleName "xtrs"
        }
    }

    sourceSets.main {
        jni.srcDirs = [] //disable automatic ndk-build call
        //jni.srcDirs = ['src/main/jni']
        jniLibs.srcDir 'src/main/libs'
    }

    buildTypes {
        debug{
            resValue "bool", "debug", "true"
        }
        release {
            resValue "bool", "debug", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi'
        }
    }
}

dependencies {
    repositories {
        mavenCentral()
    }
    compile 'com.android.support:design:23.1.+'
    compile 'com.android.support:appcompat-v7:23.1.+'
    compile 'com.android.support:mediarouter-v7:23.1.+'
    compile 'com.android.support:cardview-v7:23.1.+'
    compile 'com.android.support:recyclerview-v7:23.1.+'
    compile 'com.google.android.gms:play-services-cast:+'
    compile 'org.apache.commons:commons-io:1.3.+'
    compile 'org.simpleframework:simple-http:6.+'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.2@aar') {
        transitive = true;
    }
}

crashlytics {
    enableNdk true
    androidNdkOut 'src/main/obj'
    androidNdkLibsOut 'src/main/libs'
}
