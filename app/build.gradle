apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.0"

    defaultConfig {
        applicationId "org.puder.trs80"
        minSdkVersion 11
        targetSdkVersion 21
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

task ndkBuild(type: Exec) {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def ndkDir = properties.getProperty('ndk.dir')
        commandLine ndkDir + '/ndk-build', '-C', file('src/main/jni').absolutePath
    } else {
        print "Could not read 'local.properties'."
    }
}

   tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

dependencies {
    repositories {
        mavenCentral()
    }
    compile 'com.android.support:appcompat-v7:22.1.+'
    compile 'com.android.support:mediarouter-v7:22.1.+'
    compile 'com.android.support:cardview-v7:22.1.+'
    compile 'com.android.support:recyclerview-v7:22.1.+'
    compile 'com.google.android.gms:play-services:+'
    compile 'org.apache.commons:commons-io:1.3.+'
    compile 'org.simpleframework:simple-http:6.+'
    compile 'ch.acra:acra:4.6.+'
}
